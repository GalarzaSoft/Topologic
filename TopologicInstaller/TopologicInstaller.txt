;NSIS Modern User Interface
;Basic Example Script
;Written by Joost Verburg

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"
  !include "Sections.nsh"

;--------------------------------
;General
  !define /date MyTIMESTAMP "%Y-%m-%d-%H-%M-%S"

  ;Name and file
  Name "Topologic"
  OutFile "TopologicInstaller-${MyTIMESTAMP}.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES64\Topologic"
  
  ;Request application privileges for Windows Vista
  RequestExecutionLevel admin 

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages
  ;!define MUI_WELCOMEPAGE_TITLE "Test"
  !define MUI_WELCOMEPAGE_TEXT "This Setup will guide you through the installation of Topologic.$\r$\n$\r$\nClick Next to continue."
  
  !define MUI_ICON "Topologic-Logo-ColourOnWhite.ico"
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_BITMAP "TopoLogic-Logo.bmp"
  !define MUI_HEADERIMAGE_UNBITMAP "TopoLogic-Logo.bmp"
  !define MUI_HEADERIMAGE_RIGHT
  
  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "TopologicLicense.rtf"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections
 
Section "TopologicCore" TopologicCore

  SetOutPath "$INSTDIR"
  
  File ..\output\x64\Release\TopologicCore.dll
  File ..\output\x64\Release\TopologicUtilities.dll
  File ..\output\x64\Release\TK*.dll
  File ..\output\x64\Release\Topologic.xml
  File ..\output\x64\Release\Topologic_DynamoCustomization.xml
  File ..\output\x64\Release\TopologicEnergy.xml
  File ..\output\x64\Release\TopologicEnergy_DynamoCustomization.xml
  File ..\output\x64\Release\ShapeOp.0.1.0.dll
  File ..\output\x64\Release\license.file
  
  ;ADD YOUR OWN FILES HERE...
  
SectionEnd


Section "TopologicDynamo" TopologicDynamo

  SetOutPath "$INSTDIR"
  
  ;ADD YOUR OWN FILES HERE...
  File ..\output\x64\Release\TopologicExtensions.dll
  File ..\output\x64\Release\Topologic.dll
  ;File ..\output\x64\Release\Topologic.customization.dll
  File vc_redist.x64.exe
  
  ;Check if Revit 2017 exists.
  ClearErrors
  SetRegView 64
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Autodesk Revit 2017" "DisplayName"
  ${If} ${Errors}
    ;If Revit 2017 does not exist, check if Revit 2018 exists.
	Call CheckRevit2018
  ${Else}
    ${If} $0 == ""
	  Call CheckRevit2018
    ${Else}
	  Call CheckDynamoCore
    ${EndIf}
  ${EndIf}
  
  ;Check if Dynamo exists. If it does not, download and install.
  ClearErrors
  SetRegView 64
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Dynamo Core 2.0" "Version"
  ${If} ${Errors}
    ;Install Dynamo
	Call InstallDynamoCore
  ${Else}
    ${If} $0 == ""
      ;Install Dynamo, although this should not happen.
	  Call InstallDynamoCore
    ${EndIf}
  ${EndIf}
  
  ; Check if Visual Studio Redistributable 2017 x64 exists
  ClearErrors
  SetRegView 64
  ReadRegDWORD $0 HKLM "SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\x64" "Installed"
  ${If} ${Errors}
    ;Install Redistributable
	Call InstallVCRedistributable
  ${Else}
    ${If} $0 == ""
      ;Install Redistributable, although this should not happen.
	  Call InstallVCRedistributable
    ${Else} ; Compare version
	  ReadRegDWORD $1 HKLM "SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\x64" "Major"
	  ${If} $1 < 14
	    ;Install Redistributable
	    Call InstallVCRedistributable
	  ${Else}
	    ReadRegDWORD $2 HKLM "SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\x64" "Minor"
		${If} $2 < 14
	      ;Install Redistributable
	      Call InstallVCRedistributable
	    ${EndIf}
	  ${EndIf}
    ${EndIf}
  ${EndIf}
SectionEnd

Function CheckRevit2018
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Autodesk Revit 2018" "DisplayName"
  ${If} ${Errors}
    Call CheckRevit2019
  ${Else}
	${If} $0 == ""
	  Call CheckRevit2019
	${Else}
	  ;Revit 2019 is found. Check Dynamo Core.
	  Call CheckDynamoCore
	${EndIf}
  ${EndIf}
FunctionEnd 

Function CheckRevit2019
  ;If Revit 2018 does not exist, check if Revit 2019 exists.
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Autodesk Revit 2019" "DisplayName"
  ${If} ${Errors}
	;No Revit installation is found. Download and run Dynamo Studio 2017.
	Call InstallDynamoStudio
  ${Else}
	${If} $0 == ""
	  Call InstallDynamoStudio
	${Else}
	  ;Revit 2019 is found. Check Dynamo Core.
	  Call CheckDynamoCore
	${EndIf}
  ${EndIf}
FunctionEnd 

Function CheckDynamoCore
  ;Check if Dynamo exists. If it does not, download and install.
  ClearErrors
  SetRegView 64
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Dynamo Core 2.0" "Version"
  ${If} ${Errors}
    ;Install Dynamo
	Call InstallDynamoCore
  ${Else}
    ${If} $0 == ""
      ;Install Dynamo, although this should not happen.
	  Call InstallDynamoCore
    ${EndIf}
  ${EndIf}
FunctionEnd

Function InstallDynamoCore
  MessageBox MB_YESNO "No Dynamo Core installation is found. Press Yes to download and install it, otherwise press No to skip this proess (you will need to download and install it separately)." IDYES InstallDynamoCoreYes IDNO InstallDynamoCoreFinish
  InstallDynamoCoreYes:
    NSISdl::download http://dyn-builds-data.s3-us-west-2.amazonaws.com/DynamoInstall2.0.1.exe "$INSTDIR\DynamoInstall2.0.1.exe"
    Pop $0
    ${If} $0 == "success"
      ExecWait "$INSTDIR\DynamoInstall2.0.1.exe" $1
      Delete "$INSTDIR\DynamoInstall2.0.1.exe"
	  ${If} $1 <> 0
	    MessageBox MB_OK "Failed to install Dynamo Revit 2.0.1. Please install it manually."
	  ${EndIf}
    ${Else}
      MessageBox MB_OK "Failed to download Dynamo Revit 2.0.1. Please install it manually."
    ${EndIf}
  InstallDynamoCoreFinish:
FunctionEnd

Function InstallDynamoStudio
  MessageBox MB_YESNO "No Dynamo Studio installation is found. Press Yes to download and install it, otherwise press No to skip this process (you will need to download and install it separately)." IDYES InstallDynamoStudioYes IDNO InstallDynamoStudioFinish
  InstallDynamoStudioYes:
    NSISdl::download http://edutrial.autodesk.com/NET17SWDLD/2017/DYNSTD/14D6DC8A-A45C-43B1-B5CF-A1434EF0959A/SFX/Autodesk_Dynamo_Studio_2017_English_Win_64bit_dlm.sfx.exe "$INSTDIR\Autodesk_Dynamo_Studio_2017_English_Win_64bit_dlm.sfx.exe"
    Pop $0
    ${If} $0 == "success"
      ExecWait "$INSTDIR\Autodesk_Dynamo_Studio_2017_English_Win_64bit_dlm.sfx.exe" $1
      Delete "$INSTDIR\Autodesk_Dynamo_Studio_2017_English_Win_64bit_dlm.sfx.exe"
	  ${If} $1 <> 0
	    MessageBox MB_OK "Failed to install Dynamo Studio 2017. Please install it manually."
	  ${EndIf}
    ${Else}
      MessageBox MB_OK "Failed to download Dynamo Studio 2017. Please install it manually."
    ${EndIf}
  InstallDynamoStudioFinish:
FunctionEnd

Function InstallVCRedistributable
  MessageBox MB_YESNO "No Visual Studio 2017 Redistributale (x64) installation is found. Press Yes to install it, otherwise press No to skip this process (you will need to download and install it separately)." IDYES InstallVCRedistributableYes IDNO InstallVCRedistributableFinish
  InstallVCRedistributableYes:
    ExecWait "$INSTDIR\vc_redist.x64.exe" $0
    Delete $INSTDIR\vc_redist.x64.exe ;bug: if the installation halts (e.g. not restarted), this file is not deleted
    ${If} $0 <> 0
      MessageBox MB_OK "Failed to install Visual Studio 2017 Redistributable (x64). Please install the redistributable manually."
    ${EndIf}
  InstallVCRedistributableFinish:
FunctionEnd

Section "TopologicEnergy" TopologicEnergy

  SetOutPath "$INSTDIR"
  
  File ..\output\x64\Release\TopologicEnergy.dll
  File ..\output\x64\Release\OpenStudio.dll
  File ..\output\x64\Release\openstudio_csharp.dll
  File ..\output\x64\Release\openstudio_translators_csharp.dll
  
  ;Check if OpenStudio is already installed. Because it uses GUID, which is random, we need to iterate 
  ;through all GUIDs.
  ;https://stackoverflow.com/questions/42108679/how-can-i-find-an-application-if-i-dont-know-its-guid
  StrCpy $0 0
  Var /GLOBAL installOpenStudio
  StrCpy $installOpenStudio "0"
  loop:
    EnumRegKey $1 HKCU "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" $0
    StrCmp $1 "" done 
    ReadRegStr $2 HKCU "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$1" "DisplayName"
    ${If} $2 == "OpenStudio"
        ReadRegStr $2 HKCU "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$1" "DisplayVersion"
        ${If} $2 == "2.6.0"
		  StrCpy $installOpenStudio "1"
		  Goto done
		${EndIf}
    ${EndIf}
    IntOp $0 $0 + 1
    Goto loop

  done:
    ${If} $installOpenStudio == "0" ;not found
	  Call InstallOpenStudio
	${EndIf}
SectionEnd

Function InstallOpenStudio
  MessageBox MB_YESNO "No OpenStudio 2.6.0 installation is found. Press Yes to download and install it, otherwise press No to skip this process (you will need to download and install it separately)." IDYES InstallOpenStudioYes IDNO InstallOpenStudioFinish
  InstallOpenStudioYes:
    NSISdl::download http://openstudio-builds.s3.amazonaws.com/2.6.0/OpenStudio-2.6.0.8c81faf8bc-Windows.exe "$INSTDIR\OpenStudio-2.6.0.8c81faf8bc-Windows.exe"
    Pop $0
    ${If} $0 == "success"
      ExecWait "$INSTDIR\OpenStudio-2.6.0.8c81faf8bc-Windows.exe" $1
      Delete "$INSTDIR\OpenStudio-2.6.0.8c81faf8bc-Windows.exe"
	  ${If} $1 <> 0
	    MessageBox MB_OK "Failed to install OpenStudio 2.6.0. Please install it manually."
	  ${EndIf}
    ${Else}
      MessageBox MB_OK "Failed to download OpenStudio 2.6.0. Please install it manually."
    ${EndIf}
  InstallOpenStudioFinish:
FunctionEnd

Section #hidden, run after all other sections
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  
  ;Install and run Streams
  File streams.exe
  ExecWait "$INSTDIR\streams.exe -s -d *"
  Delete $INSTDIR\streams.exe
SectionEnd

Function .onInit
  IntOp $R0 ${SF_RO} | ${SF_SELECTED}
  SectionSetFlags ${TopologicCore} $R0
FunctionEnd

Function .onSelChange
  ${If} $0 == 1 ;TopologicDynamo
    ;If TopologicDynamo is selected, also select TopologicUtility.
    SectionGetFlags ${TopologicDynamo} $R0
    IntOp $R0 $R0 & ${SF_SELECTED}
    ${If} $R0  == 0
      ;If TopologicDynamo is deselected, also deselect TopologicEnergy.
      SectionGetFlags ${TopologicEnergy} $R1
	  IntOp $R1 $R1 & ~${SF_SELECTED}
	  SectionSetFlags ${TopologicEnergy} $R1
    ${EndIf}
  ${ElseIf} $0 == 2 ;TopologicEnergy
    ;If TopologicEnergy is selected, also select TopologicDynamo and TopologicUtility.
    SectionGetFlags ${TopologicEnergy} $R0
    IntOp $R0 $R0 & ${SF_SELECTED}
    ${If} $R0 != 0
      SectionGetFlags ${TopologicDynamo} $R1
	  IntOp $R1 $R1 | ${SF_SELECTED}
	  SectionSetFlags ${TopologicDynamo} $R1
	
	  ;SectionGetFlags ${TopologicUtility} $R1
	  ;IntOp $R1 $R1 | ${SF_SELECTED}
	  ;SectionSetFlags ${TopologicUtility} $R1
    ${EndIf}
  ${EndIf}
  
FunctionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_TopologicCore ${LANG_ENGLISH} "Installs the native core layer of Topologic."
  LangString DESC_TopologicDynamo ${LANG_ENGLISH} "Installs the Dynamo layer of Topologic."
  LangString DESC_TopologicEnergy ${LANG_ENGLISH} "Installs the Energy application layer of Topologic."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${TopologicCore} $(DESC_TopologicCore)
    !insertmacro MUI_DESCRIPTION_TEXT ${TopologicDynamo} $(DESC_TopologicDynamo)
    !insertmacro MUI_DESCRIPTION_TEXT ${TopologicEnergy} $(DESC_TopologicEnergy)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;ADD YOUR OWN FILES HERE...
  Delete "$INSTDIR\*"
  Delete "$INSTDIR\Uninstall.exe"
  RMDir "$INSTDIR"

SectionEnd